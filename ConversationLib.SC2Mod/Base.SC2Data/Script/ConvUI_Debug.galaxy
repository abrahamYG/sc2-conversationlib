include "Script/ConvUI/Callbacks/header";
//include "Script/ConvUI/Modules/init";
include "Script/ConvUI/Modules/Transmission/header";
include "Script/ConvUI/Modules/Briefing/header";
include "Script/ConvUI/Modules/LineDialogs/header";

static const string CONVUI_CONVERSATIONID = "TestConversation";
static const string CONVUI_DEBUGBUTTONPATH = "UIContainer/FullscreenUpperContainer/ConvoButton0";

static bool ConvUI_DebugConversationInitialized = false;
static bool ConvUI_DebugCharactersInitialized = false;
static bool ConvUI_DebugCallbacksInitialized = false;
static bool ConvUI_DebugButtonsInitialized = false;

CConversation ConvUI_DebugConversation;
ConversationCallback ConvUI_DebugCallbacks;
int[3] ConvUI_DebugButton;



void ConvUI_DebugLineCallback(CConversationRef Conversation, SConversationLineRef Line, CCharacterRef Character, int index, int player){
	TriggerDebugOutput(1,StringToText("Line: "+Line.Id),true);
}

void ConvUI_DebugWaitCallback(CConversationRef Conversation, SConversationWaitRef WaitRef, int index, int player){
    TriggerDebugOutput(1,StringToText("WaitRef: "+WaitRef.Id),true);
    ConvUI_DefaultWaitCallback(Conversation,WaitRef,index,player);
}

void ConvUI_DebugChoiceCallback(CConversationRef Conversation, SConversationChoiceRef Choice, CCharacterRef Character, int index, int player){
    TriggerDebugOutput(1,StringToText("Choice: "+Choice.Id),true);
}

void ConvUI_DebugRunActionsCallback(CConversationRef Conversation, SConversationRunActionsRef RunActions, int index, int player){
    TriggerDebugOutput(1,StringToText("RunActions: "+RunActions.Id),true);
}
void ConvUI_DebugCommentCallback(CConversationRef Conversation, SConversationCommentRef Comment, int index, int player){
    TriggerDebugOutput(1,StringToText("Comment: "+Comment.Id),true);
}
void ConvUI_DebugJumpCallback(CConversationRef Conversation, SConversationJumpRef Jump, int index, int player){
    TriggerDebugOutput(1,StringToText("Jump: "+Jump.Id),true);
}

void ConvUI_DebugGroupCallback(CConversationRef Conversation, SConversationGroupRef Group, int index, int player){
    TriggerDebugOutput(1,StringToText("Group: "+Group.Id),true);
}


void ConvUI_InitDebugConversation(){
    if(ConvUI_DebugConversationInitialized){return;}
    ConvUI_CConversationCreateFromData(ConvUI_DebugConversation,CONVUI_CONVERSATIONID);
    ConvUI_DebugConversationInitialized = true;
}

void ConvUI_InitDebugCharacters(){
    if(ConvUI_DebugCharactersInitialized){return;}
    ConvUI_CCharacterCreateFromCatalog(ConvUI_CCharacters);
    ConvUI_DebugCharactersInitialized = true;
}


void ConvUI_SetDebugCallbacks(ConversationCallbackRef Callbacks){
    Callbacks.LineCallback = ConvUI_DebugLineCallback;
    Callbacks.WaitCallback = ConvUI_DebugWaitCallback;
    Callbacks.ChoiceCallback = ConvUI_DebugChoiceCallback;
    Callbacks.RunActionsCallback = ConvUI_DebugRunActionsCallback;
    Callbacks.CommentCallback = ConvUI_DebugCommentCallback;
    Callbacks.JumpCallback = ConvUI_DebugJumpCallback;
    Callbacks.GroupCallback = ConvUI_DebugGroupCallback;
}

void ConvUI_InitDebugCallbacks(){
    if(ConvUI_DebugCallbacksInitialized){return;}
    ConvUI_SetDebugCallbacks(ConvUI_DebugCallbacks);
    ConvUI_DebugCallbacksInitialized = true;
}




void ConvUI_RunDataConversation(CConversationRef Conversation){
    playergroup pg = PlayerGroupActive();
	int player = PlayerGroupPlayer(pg,1);
    int Portrait1 = libNtve_gf_CinematicPortrait(libNtve_ge_CinematicPortraitPosition_CenterLeft);
    int Portrait2 = libNtve_gf_CinematicPortrait(libNtve_ge_CinematicPortraitPosition_CenterRight);
    string CharacterId1 = Conversation.DefaultSpeaker1;
    string CharacterId2 = Conversation.DefaultSpeaker2;

    libNtve_gf_CinematicMode(true, pg, c_transitionDurationImmediate);
    ConversationDataPreloadLines(Conversation.Id);
    ConversationDataRegisterPortrait(CharacterId1, Portrait1);
    ConversationDataRegisterPortrait(CharacterId2, Portrait2);
    ConversationDataRun(Conversation.Id,pg, c_conversationSkipFull, true);
    libNtve_gf_CinematicMode(false, pg, c_transitionDurationImmediate);
}


bool ConvUI_DebugButtonFunc(bool a, bool b){
    int player = EventPlayer();
    playergroup pg = PlayerGroupSingle(player);
    CConversationRef Conversation = ConvUI_DebugConversation;
    arrayref<CCharacterArray> Characters = ConvUI_CCharacters;
    ConversationCallbackRef Callbacks = ConvUI_DebugCallbacks;
    int button = EventDialogControl();
    if(button==ConvUI_DebugButton[0]){
        ConvUI_SetManagedCallbackModule(Conversation, Callbacks, "Transmission");
    }
    else if(button==ConvUI_DebugButton[1]){
        ConvUI_SetManagedCallbackModule(Conversation, Callbacks, "Briefing");
    }
    else if(button==ConvUI_DebugButton[2]){
        ConvUI_SetManagedCallbackModule(Conversation, Callbacks, "LineDialogs");
    }
    else{
        return false;
    }
    ConvUI_ConversationRun(Conversation,Characters,Callbacks,player);
    //ConvUI_RunDataConversation(Conversation);
    return true;
}


void ConvUI_InitDebugButtons(){
    int i = 0;
    trigger t;
    int player = 1;
    playergroup pg = PlayerGroupSingle(player);
    const int BUTTON_COUNT = 3;
    if(ConvUI_DebugButtonsInitialized){return;}
    t = TriggerCreate("ConvUI_DebugButtonFunc");
    for(i=0; i<BUTTON_COUNT; i+=1){
        ConvUI_DebugButton[i] = DialogControlHookupStandard(c_triggerControlTypeButton,CONVUI_DEBUGBUTTONPATH+IntToString(i+1));
        TriggerAddEventDialogControl(t,player,ConvUI_DebugButton[i],c_triggerControlEventTypeClick);
    }
    ConvUI_DebugButtonsInitialized = true;
}


bool ConvUI_DebugRunConversationFunc(bool a, bool b){
    int player = EventPlayer();
    CConversationRef Conversation = ConvUI_DebugConversation;
    arrayref<CCharacterArray> Characters = ConvUI_CCharacters;
    ConversationCallbackRef Callbacks = ConvUI_DebugCallbacks;
    
    ConvUI_ConversationRun(Conversation,Characters,Callbacks,player);
    return true;
}



//I wish i knew why i set this as a trigger instead of a regular func, if you as me, maybe i did so i could rerun it on demand
bool ConvUI_DebugInitTriggerFunc(bool a, bool b){
    ConvUI_InitDebugConversation();
    ConvUI_InitDebugCharacters();
    ConvUI_InitDebugCallbacks();
    ConvUI_InitDebugButtons();

    return true;
}
//Init funtion,must be run, as guessed, on map init
void ConvUI_DebugInit(){
    trigger t;
    t = TriggerCreate("ConvUI_DebugInitTriggerFunc");
    TriggerExecute(t,false,false);
    t = TriggerCreate("ConvUI_DebugRunConversationFunc");
    
}